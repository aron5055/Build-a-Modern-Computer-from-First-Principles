// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    And16(a=x, b=false, out=zerox);
    Mux16(a=x, b=zerox, sel=zx, out=rx1);
    Not16(in=rx1, out=nrx1);
    Mux16(a=rx1, b=nrx1, sel=nx, out=rx2);

    And16(a=y, b=false, out=zeroy);
    Mux16(a=y, b=zeroy, sel=zy, out=ry1);
    Not16(in=ry1, out=nry1);
    Mux16(a=ry1, b=nry1, sel=ny, out=ry2);

    And16(a=rx2, b=ry2, out=andxy);
    Add16(a=rx2, b=ry2, out=sumxy);
    Mux16(a=andxy, b=sumxy, sel=f, out=res);

    Not16(in=res, out=nres);
    Mux16(a=res, b=nres, sel=no, out=out, out[0..7]=outlow, out[8..15]=outhigh, out[15]=finalbit);

    Mux(a=false, b=true, sel=finalbit, out=ng);
    Or8Way(in=outlow, out=low);
    Or8Way(in=outhigh, out=high);
    Or(a=low, b=high, out=notzr);
    Not(in=notzr, out=zr);
}